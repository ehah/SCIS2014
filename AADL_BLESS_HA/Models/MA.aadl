
--Below is model of the Movement Authority scenario of Chinese Train Control System level 3 (CTS-3)
package MA
public
with CTCS_Types, CTCS_Properties, BLESS;
system sysMA
end sysMA; 
system implementation sysMA.impl
subcomponents
Train: abstract Train.impl;
pController: process pController.impl;
RBC : device RBC.impl;
connections
conn_position: port Train.ts -> pController.ps;
conn_velocity: port Train.tv -> pController.pv;
conn_acc: port pController.pa -> Train.ta;
conn_req: port pController.preq -> RBC.r;
conn_ma: port RBC.m -> pController.pma;
conn_ea: port RBC.ea -> pController.ea;
end sysMA.impl;

-----Train abstract component---------
abstract Train
  features
   
    ts : out data port CTCS_Types::Position ; --{BLESS::Assertion=> "<<:=POSITION>>";};
    tv : out data port CTCS_Types::Velocity ; --{BLESS::Assertion=> "<<:=VELOCITY>>";};
 	ta : in data port CTCS_Types::Acceleration ; 
 
 end Train;

abstract implementation Train.impl
annex hybrid{**
	
  variables
   s : CTCS_Types::Position -- train position
   v : CTCS_Types::Velocity -- train velocity
   a : CTCS_Types::Acceleration --train acceleration
   t : CTCS_Types::Time     -- train clock time
  constants
   b = 1 mpss    --maximum deceleration of the train, assumed as 1 mpss
  behavior
   Train ::= 'DT 1 s = v' & 'DT 1 v = a' & 'DT 1 t = 1'
             [[> ts!(s), tv!(v),ta?(a) ]]> Continue
   Continue ::= skip
   RunningTrain ::= s:= 0 & v:=0 & a:=0 & REPEAT(Train)
**};
end Train.impl;

-----Controller process and thread----
process pController
features
ps: in data port CTCS_Types::Position; 
pv: in data port CTCS_Types::Velocity; 
pa : out data port  CTCS_Types::Acceleration;     --check - {BLESS::Assertion=> "<<TDELAY()>>";};
preq: out event port ; --check - {BLESS::Assertion=> "<<s=0 or s>=iSeg.e>>";};  --make sure iSeg must be the second last segment here, req has EOA
ea: in event data port CTCS_Types::EOA;
pma: in event data port CTCS_Types::MovementAuthority; --check - {BLESS::Assertion=> "<<CHECKV1()>>";} ;
end pController;
process implementation pController.impl
subcomponents
Controller: thread Controller.impl;
connections
conn_s: port ps->Controller.cs; 
conn_v: port pv->Controller.cv;
conn_a: port Controller.ca ->pa;
conn_req: port Controller.r->preq;
conn_ma: port pma->Controller.m;
conn_ea: port  ea ->Controller.ea;
end pController.impl;

------Controller Thread-------------
thread Controller
features
r: out event port  {BLESS::Assertion=> "<<(s=CTCS_Properties::start) or (s>=(e-SR))>>";};  
m: in event data port CTCS_Types::MovementAuthority {BLESS::Assertion=> "<<:=IMA>>";} ;
ea: in event data port CTCS_Types::EOA;
cs: in data port CTCS_Types::Position {BLESS::Assertion=> "<<:=POSITION>>";}; 
cv: in data port CTCS_Types::Velocity {BLESS::Assertion=> "<<:=VELOCITY>>";};  
ca : out data port  CTCS_Types::Acceleration {BLESS::Assertion=> "<<:=Acceleration()>>";};   
msg: out event data port CTCS_Types::DMI_Message {BLESS::Assertion=>"<<iMA=null>>";}; --message to DMI
properties
Dispatch_Protocol => Periodic;  
Period => 200 ms;
end Controller;

thread implementation Controller.impl
annex BLESS{**
assert 
	--<<SRL:x: exists t:CTCS_Types::Time in x-sr .. x that (s@t and v@t)>> --sampling rate limit
	<<SBL: :  (s=CTCS_Properties::start) or ((v < iSeg.v2))>> --service brake limit
	<<EBL: :  (v < iSeg.v1) >> --emergency brake limit
	<<DSPV1: : (((v**2) + (2*b*s)) < (iMA[nSeg.v1] + (2*b*iSeg.e))) >> --DSP for V1
	<<DSPV2: : (s=CTCS_Properties::start) or  (((v**2) + (2*b*s)) < (iMA[nSeg.v2] + (2*b*iSeg.e))) >> --DSP for V2
	
  <<Acceleration: :=
  (NOCHANGE()) -> 0,                                 			
  (not SBL() or not DSPV2()) -> CTCS_Properties::SB_Rate, 
  (not EBL() or not DSPV1()) -> -b,   --CTCS_Properties::EB_Rate = b   			
  (SBL() and DSPV2()) -> xl >> 
  
invariant 
	<<true>>
	
variables
	i: Base_Types::Integer:=0; 
	b : CTCS_Types::Deceleration; 
	v : CTCS_Types::Velocity:=0;
	s : CTCS_Types::Position;
	e : CTCS_Types::EOA;
	xl: CTCS_Types::Acceleration;
	iMA : CTCS_Types::MovementAuthority:=null;
	iSeg, nSeg : CTCS_Types::Segment:=null;
	
states 
	READY : initial state <<(i=0) and (s=CTCS_Properties::start)>> ; 
	GMA   : complete state  <<(i=0) and (s=CTCS_Properties::start)>> ; 
	CMA   : state <<(i=0) and (s=CTCS_Properties::start)>>;
	RETRY : state <<(i=0) and (s=CTCS_Properties::start)>>;
	MFR   : complete state << SBL() and DSPV2()>>;
	CMF   : state <<SBL() and DSPV2()>>;
	SBI   : complete state << not SBL() or not DSPV2() >>; 
	CSB   : state << not SBL() or not DSPV2() >>;
	EBI   : complete state << not EBL() or not DSPV1()  >>;
	STOP  : final state << (not EBL() or not DSPV1()) and v=0>>; 
																	
 transitions
	T0_go: READY -[]-> GMA { 
							<<(i=0) and (s=CTCS_Properties::start)>> 
							r!; 
							<<(i=0) and (s=CTCS_Properties::start)>> 
							(i',s':=i,s) 
							<<(i'=0) and (s'=CTCS_Properties::start)>>
							}; 
	
	T1_MA_Check: GMA -[on dispatch]-> CMA {
											m?(iMA) ; <<(i=0) and (s=CTCS_Properties::start) and (iMA=IMA) >> 
											ea?(e) <<(i=0) and (s=CTCS_Properties::start) and (iMA=IMA) >>
										}; 
	
	
	T2_MA_Ok: CMA -[not (iMA=null)]-> MFR{
										   (iSeg',nSeg',i',v',s',b',iMA':=iSeg, nSeg,i,v,s,b,iMA) 
										   <<(SBL() and DSPV2())^1>>
										  };
	 
    
		
	T3_Move_Check: MFR -[on dispatch]-> CMF{ { <<SBL() and DSPV2()>> 
											cs?(s) <<s=POSITION>> & cv?(v) 	<<SBL() and DSPV2() and v=VELOCITY>> };
											
											 <<SBL() and DSPV2()>>
											 if ( s>=(e-SR) )~>
    											 <<SBL() and DSPV2() and (s>=(e-SR))>>
    											  r!   --- how to check the MA is extended or not
    											<<SBL() and DSPV2()>>	
    										[]
    										   (not (s>=(e-SR)) )~>
    										   	 <<SBL() and DSPV2()>>
    											 skip
    											 <<SBL() and DSPV2()>>	
    										    										
    										fi  <<SBL() and DSPV2()>> 
    										
    										};
    											
	T4_SBI_Point:  CMF -[not ((s=CTCS_Properties::start) or ((v < iSeg.v2))) or
						not ((s=CTCS_Properties::start) or  ((((v**2) + (2*b*s)) < (iMA[nSeg.v2] + (2*b*iSeg.e)))))]-> SBI
												{
												<<not SBL() or not DSPV2()>> ca!(CTCS_Properties::SB_Rate); <<not SBL() or not DSPV2()>>
												(iSeg',nSeg',i',v',s',b',iMA':=iSeg, nSeg,i,v,s,b,iMA) 
												<<(not SBL() or not DSPV2())^1>>
												};  
	
	T5_Move_Ok: CMF -[( (s=CTCS_Properties::start) or ((v < iSeg.v2))) or  (((v**2) + (2*b*s)) < (iMA[nSeg.v2] + (2*b*iSeg.e)))]-> MFR
									{
									<<SBL() and DSPV2()>>  ca!(xl); <<SBL() and DSPV2()>> 
									(i,iSeg,nSeg:=i+1,nSeg,iMA[i+2]); <<SBL() and DSPV2()>>   -- What if the MA is extended and what if not, have to update values accordingly
	  								(iSeg',nSeg',i',v',s',b', iMA':=iSeg, nSeg,i,v,s,b, iMA)<<(SBL() and DSPV2())^1>>
	  								}; 
  
  	 
	T6_SBI_Check: SBI -[on dispatch]-> CSB { 
											{<<not SBL() or not DSPV2()>>
											cs?(s) <<s=POSITION>> & cv?(v) 	
											<<(not SBL() or not DSPV2()) and (v=VELOCITY)>> }
										   };
	
	
	T7_SBI_Out: CSB-[ ((s=CTCS_Properties::start) or ((v < iSeg.v2))) and ((s=CTCS_Properties::start) or 
					((((v**2) + (2*b*s)) < (iMA[nSeg.v2] + (2*b*iSeg.e))))) ]-> MFR
		       					{
		       					 <<SBL() and DSPV2()>>
		        				 ca!(xl); 
		      					 <<SBL() and DSPV2()>>
								 (iSeg',nSeg',i',v',s',b', iMA':=iSeg, nSeg,i,v,s,b, iMA)
								 <<(SBL() and DSPV2())^1>>
								 };
		
	
	T8_SBI_Ok: CSB -[not (((s=CTCS_Properties::start) or ((v < iSeg.v2))) and ((s=CTCS_Properties::start) or 
					((((v**2) + (2*b*s)) < (iMA[nSeg.v2] + (2*b*iSeg.e)))))) ]-> SBI
								 {
								 (iSeg',nSeg',i',v',s',b',iMA':=iSeg, nSeg,i,v,s,b,iMA)
								 <<(not SBL() or not DSPV2())^1>>
								 };

	
	T9_MA_NotOk: CMA-[iMA=null]-> RETRY {msg!};
 
    T10_MA_Retry: RETRY -[]-> GMA {(i',s':=i,s)};
	
	T11_EBI_Point: CMF -[not (v < iSeg.v1) or not ((((v**2) + (2*b*s)) < (iMA[nSeg.v1] + (2*b*iSeg.e)))) ]-> EBI
							{
							<<not EBL() or not DSPV1()>> ca!(-b); <<not EBL() or not DSPV1()>> --ca!(CTCS_Properties::EB_Rate)
							(iSeg',nSeg',i',v',s',b',iMA':=iSeg, nSeg,i,v,s,b,iMA) 
							<<(not EBL() or not DSPV1())^1>>
							};  
	T12_Stop: EBI -[v=0]-> STOP {};
	
	T13_EBI_Point: SBI -[not (v < iSeg.v1) or not ((((v**2) + (2*b*s)) < (iMA[nSeg.v1] + (2*b*iSeg.e))))]-> EBI
						   {
							<<not EBL() or not DSPV1()>> ca!(-b); <<not EBL() or not DSPV1()>> --ca!(CTCS_Properties::EB_Rate)
							(iSeg',nSeg',i',v',s',b',iMA':=iSeg, nSeg,i,v,s,b,iMA) 
							<<(not EBL() or not DSPV1())^1>>
						   };
  **};

end Controller.impl;



-----RBC Device----
device RBC
features
r: in event port;
m: out event data port CTCS_Types::MovementAuthority;
ea: out data port CTCS_Types::EOA;
end RBC; 
device implementation RBC.impl
end RBC.impl;

end MA;

